/********************
 * Extra Text
 ********************/

switch(FEAT_INDUSTRIES, SELF, setDword, low_bits, high_bits, register, [
    STORE_TEMP(
        (low_bits) | (high_bits << 16),
        register
    )
]) { return; }


switch(FEAT_INDUSTRIES, SELF, setPowerStatus, power_pct, [
    power_pct
]) {
    0:       SET_TEMP(POWER_STATUS, string(STR_POWER_STATUS_NONE));
    1..99:   SET_TEMP(POWER_STATUS, string(STR_POWER_STATUS_LOW));
    default: SET_TEMP(POWER_STATUS, string(STR_EMPTY));
}

switch(FEAT_INDUSTRIES, SELF, genExtraText, [
    (power_required == 1) ? [
        SET_TEMP(POWER_PCT, getPowerSuppliedPct()),
        setPowerStatus(GET_TEMP(POWER_PCT))
    ]:[
        SET_TEMP(POWER_PCT, 100),
        SET_TEMP(POWER_STATUS, string(STR_EMPTY)),
    ],

    setDword(
        GET_TEMP(POWER_STATUS),
        OUTPUT_RATE_MAX * 8 * GET_TEMP(POWER_PCT) / 100,
        256),
    setDword(
        OUTPUT_RATE_MAX * 9 * GET_TEMP(POWER_PCT) / 100,
        GET_PERM(PRODUCTION_RATE) * GET_TEMP(POWER_PCT) / PRODUCTION_MAX,
        257),
    setDword(
        GET_PERM(STOCKPILE_AVERAGE),
        THREE_MONTH_PROD_TICKS * GET_PERM(PRODUCTION_RATE),
        258),

    (show_prod_breakdown == 1) ? [
        setDword(
            string(STR_PROD_BREAKDOWN),
            GET_PERM(STOCKPILE_IMPACT) * 100 / GET_PERM(PRODUCTION_RATE_PREV),
            259
        ),
        STORE_TEMP(
            GET_PERM(TRANSPORTED_IMPACT) * 100 / GET_PERM(PRODUCTION_RATE_PREV),
            260
        )
    ]:[0]
]) {
    return string(STR_INDUSTRY_EXTRA_TEXT);
}


switch(FEAT_INDUSTRIES, SELF, getGridStatus, [
    getPowerSuppliedPct()
]) {
    0:       SET_TEMP(GRID_STATUS, string(STR_GRID_NO_POWER));
    1..99:   SET_TEMP(GRID_STATUS, string(STR_GRID_OVERLOADED));
    default: SET_TEMP(GRID_STATUS, string(STR_GRID_HEALTHY));
}

switch(FEAT_INDUSTRIES, SELF, genExtraTextPowerPlant, [
    (hasPowerDemand())
        ? getGridStatus()
        : SET_TEMP(GRID_STATUS, string(STR_GRID_NO_DEMAND)),
    SET_TEMP(PROD_PCT, GET_PERM(PRODUCTION_RATE) * 100 / PRODUCTION_MAX),
    (GET_TEMP(PROD_PCT) == 0 && GET_PERM(PRODUCTION_RATE) != 0)
        ? SET_TEMP(PROD_PCT, 1)
        : 0,
    setDword(
        GET_TEMP(PROD_PCT),
        GET_TEMP(GRID_STATUS),
        256),
    STORE_TEMP(
        GET_PERM(STOCKPILE_AVERAGE),
        257),
]) {
    return string(STR_INDUSTRY_EXTRA_TEXT_POWER_PLANT);
}
