/********************
 * Extra Text
 ********************/

switch(FEAT_INDUSTRIES, SELF, setDword, low_bits, high_bits, register, [
    STORE_TEMP(
        (low_bits & 0xFFFF) | (high_bits << 16),
        register
    )
]) { return; }


switch(FEAT_INDUSTRIES, SELF, setPowerStatus, power_pct, [
    power_pct
]) {
    0:       SET_TEMP(POWER_STATUS, string(STR_POWER_STATUS_NONE));
    1..99:   SET_TEMP(POWER_STATUS, string(STR_POWER_STATUS_LOW));
    default: SET_TEMP(POWER_STATUS, string(STR_EMPTY));
}

switch(FEAT_INDUSTRIES, SELF, setProductionCapacity, [
    (power_required == 1) ? [
        SET_TEMP(POWER_PCT, getPowerSuppliedPct()),
        setPowerStatus(GET_TEMP(POWER_PCT))
    ]:[
        SET_TEMP(POWER_PCT, 100),
        SET_TEMP(POWER_STATUS, string(STR_EMPTY)),
    ],
    setDword(
        GET_TEMP(POWER_STATUS),
        GET_PERM(PRODUCTION_RATE) * GET_TEMP(POWER_PCT) / PRODUCTION_MAX,
        256),
]) { return; }

switch(FEAT_INDUSTRIES, SELF, setProductionBreakdown, base_reg, [
    (show_prod_breakdown == 1) ? [
        setDword(
            string(STR_PROD_BREAKDOWN),
            GET_PERM(IMPACT_STOCKPILE) * 100 / GET_PERM(PRODUCTION_RATE_PREV),
            base_reg
        ),
        STORE_TEMP(
            GET_PERM(IMPACT_TRANSPORTED) * 100 / GET_PERM(PRODUCTION_RATE_PREV),
            base_reg + 1
        )
    ]:[0]
]) { return; }

switch(FEAT_INDUSTRIES, SELF, setProductionBreakdownHalfStart,
    low_bits,
    base_reg,
[
    (show_prod_breakdown == 0) ? [
        STORE_TEMP(
            low_bits,
            base_reg)
    ]:[
        setDword(
            low_bits,
            string(STR_PROD_BREAKDOWN),
            base_reg),
        setDword(
            GET_PERM(IMPACT_STOCKPILE) * 100 / GET_PERM(PRODUCTION_RATE_PREV),
            GET_PERM(IMPACT_TRANSPORTED) * 100 / GET_PERM(PRODUCTION_RATE_PREV),
            base_reg + 1),
    ]
]) { return; }


switch(FEAT_INDUSTRIES, SELF, genExtraText, cargo_name, [
    setProductionCapacity(),
    setDword(
        cargo_name,
        getStockpileAverage(0),
        257),
    setProductionBreakdownHalfStart(
        STOCKPILE_REQUIRED,
        258)
]) {
    return string(STR_INDUSTRY_EXTRA_TEXT);
}

switch(FEAT_INDUSTRIES, SELF, temperate_factory_genExtraText, [
    setProductionCapacity(),
    setDword(
        TTD_STR_CARGO_PLURAL_LIVESTOCK,
        getStockpileAverage(0),
        257),
    setDword(
        TTD_STR_CARGO_PLURAL_GRAIN,
        getStockpileAverage(1),
        258),
    setDword(
        TTD_STR_CARGO_PLURAL_STEEL,
        getStockpileAverage(2),
        259),
    setProductionBreakdownHalfStart(
        STOCKPILE_REQUIRED,
        260)
]) {
    return string(STR_INDUSTRY_EXTRA_TEXT_FACTORY);
}

switch(FEAT_INDUSTRIES, SELF, getGridStatus, [
    getPowerSuppliedPct()
]) {
    0:       SET_TEMP(GRID_STATUS, string(STR_GRID_NO_POWER));
    1..99:   SET_TEMP(GRID_STATUS, string(STR_GRID_OVERLOADED));
    default: SET_TEMP(GRID_STATUS, string(STR_GRID_HEALTHY));
}

switch(FEAT_INDUSTRIES, SELF, power_plant_genExtraText, [
    (hasPowerDemand())
        ? getGridStatus()
        : SET_TEMP(GRID_STATUS, string(STR_GRID_NO_DEMAND)),

    SET_TEMP(PROD_PCT, GET_PERM(PRODUCTION_RATE) * 100 / PRODUCTION_MAX),
    (GET_TEMP(PROD_PCT) == 0 && GET_PERM(PRODUCTION_RATE) != 0)
        ? SET_TEMP(PROD_PCT, 1)
        : 0,

    setDword(
        GET_TEMP(PROD_PCT),
        GET_TEMP(GRID_STATUS),
        256),
    setDword(
        TTD_STR_CARGO_PLURAL_COAL,
        GET_PERM(STOCKPILE_AVERAGE_0),
        257),
    setDword(
        TTD_STR_CARGO_PLURAL_OIL,
        GET_PERM(STOCKPILE_AVERAGE_1),
        258),
]) {
    return string(STR_INDUSTRY_EXTRA_TEXT_POWER_PLANT);
}
