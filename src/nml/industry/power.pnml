#ifndef POWER_PNML
#define POWER_PNML

#include "registers.pnml"

/* adjustPowerDemand(demand_change) */
switch(FEAT_INDUSTRIES, PARENT, adjustPowerDemand, demand_change, [
    SET_PERM(POWER_DEMAND_TOTAL, GET_PERM(POWER_DEMAND_TOTAL) + demand_change)
]) { return; }


// power required per unit of cargo produced
#define DEMAND_PER_CARGO_UNIT 250

/* setPowerDemand(cargo_produced) */
switch(FEAT_INDUSTRIES, SELF, setPowerDemand, cargo_produced, [
    SET_TEMP(DEMAND_PRODUCED,
        cargo_produced * DEMAND_PER_CARGO_UNIT
    ),
    // idling capacity demands 10% of active production power
    SET_TEMP(DEMAND_IDLE,
        (OUTPUT_RATE_MAX - cargo_produced) * DEMAND_PER_CARGO_UNIT / 10
    ),

    SET_TEMP(DEMAND_CHANGE,
        GET_TEMP(DEMAND_PRODUCED)
        + GET_TEMP(DEMAND_IDLE)
        - GET_PERM(POWER_DEMAND_PREV)
    ),

    // WARNING: For reasons probably best known to NFO wizards,
    // adjustPowerDemand() won't work if there isn't some sort of
    // STORE_PERM() command prior to it.
    SET_PERM(POWER_DEMAND_PREV,
        GET_TEMP(DEMAND_PRODUCED) + GET_TEMP(DEMAND_IDLE)
    ),

    adjustPowerDemand(GET_TEMP(DEMAND_CHANGE))
]) { return; }

#endif // POWER_PNML
